apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group='com.tiagosantos'
version = '1.0.0'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'EchantedViewPager'
            description 'A custom ViewPager that provides some custom and usefull functionalities, such as, scalling scroll, scrolling with alpha values and swipe to dismiss option.'
            url 'https://github.com/googlesamples/easygoogle'
            inceptionYear '2015'

            packaging 'aar'
            groupId 'com.tiagosantos'
            artifactId 'echantedviewpager'
            version '1.0.0'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/TMSantos/echantedviewpager.git'
                url 'https://github.com/TMSantos/echantedviewpager'

            }
            developers {
                developer {
                    name 'Tiago Santos'
                }
            }
        }
    }
}

bintray {
    user = project.property('bintrayUser')
    key = project.property('bintrayApiKey')
    pkg {
        repo = 'maven'
        name = 'com.tiagosantos.echantedviewpager'

        version {
            name = '1.0.0'
            desc = 'EchantedViewPager is a custom ViewPager that provides some custom and usefull functionalities, such as, scalling scroll, scrolling with alpha values and swipe to dismiss option.'
            released  = new Date()
            vcsTag = '1.0.0'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/TMSantos/echantedviewpager.git'
        websiteUrl = 'https://github.com/TMSantos/echantedviewpager'
    }
    configurations = ['archives']
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.0.0'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}
